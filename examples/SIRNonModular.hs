{-# LANGUAGE DataKinds           #-}
{-# LANGUAGE FlexibleContexts    #-}
{-# LANGUAGE OverloadedLabels    #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE TypeApplications    #-}
{-# LANGUAGE TypeFamilies        #-}
{-# LANGUAGE TypeOperators       #-}
{-# OPTIONS_GHC -Wno-unrecognised-pragmas #-}
{-# HLINT ignore "Redundant return" #-}
{-# LANGUAGE AllowAmbiguousTypes #-}

{- |
     This demonstrates:
      - The [SIR](https://en.wikipedia.org/wiki/Compartmental_models_in_epidemiology) model for modelling
        the transition between Susceptible (S), Infected (I), and Recovered (R) individuals during an epidemic.
        We model this as a Hidden Markov Model, where the _latent states_ are the true values of S, I, and R,
        and the _observations_ are the reported number of infections (ùúâ).
      - Extending the SIR to the SIRS model where recovered individuals (R) can become susceptible (S) again.
      - Extending the SIRS to the SIRSV model where susceptible individuals (S) can become vaccinated (V).

      Note that the extensions (SIRS and SIRSV) aren't as modular as we would like, due to having to
      redefine the data types Popl and TransParams when adding new variables to the SIR model.
      The file [SIRModular](examples/SIR.hs) shows how one could take steps to resolve this by using
      extensible records.
-}

module SIRNonModular where

import           Control.Algebra               (Has)
import           Control.Carrier.Writer.Strict (WriterC, runWriter, tell)
import           Control.Effect.Sum            ((:+:), Member)
import           Control.Effect.Writer         (Writer)
import           Control.Monad                 ((>=>))
import           Env                           (Assign ((:=)), Env, Observable,
                                                Observables, get, nil, (<:>))
import           HMM                           (ObsModel, TransModel, hmmGen)
import           Inference.MH                  as MH (mhRaw)
import           Inference.SIM                 as SIM (simulate)
import           Model                         (Model (Model, runModel), beta, binomial', gamma,
                                                poisson)
import           Sampler                       (Sampler)

runWriterM :: forall env w sig m a. Monoid w =>  Model env (Writer w :+: sig) (WriterC w m) a -> Model env sig m (w, a)
runWriterM model = Model $ runWriter $ runModel model

{- | SIR model.
-}

-- | SIR model environment
type SIRenv =
 '[ "Œ≤"  := Double  -- ^ mean contact rate between susceptible and infected people
  , "Œ≥"  := Double  -- ^ mean recovery rate
  , "œÅ"  := Double  -- ^ mean report rate of infection
  , "ùúâ"  := Int     -- ^ number of reported infections
 ]

-- | Latent state
data Popl = Popl {
    s :: Int, -- ^ number of people susceptible to infection
    i :: Int, -- ^ number of people currently infected
    r :: Int  -- ^ number of people recovered from infection
} deriving Show

-- | Transition model parameters
data TransParamsSIR = TransParamsSIR {
    betaP  :: Double, -- ^ mean contact rate between susceptible and infected people
    gammaP :: Double  -- ^ mean recovery rate
}

-- | Observation ùúâ
type Reported = Int

-- | Observation model parameters
type ObsParams = Double

-- | Transition model prior
transPriorSIR :: Observables env '["Œ≤",  "Œ≥"] Double
  => Model env sig m TransParamsSIR
transPriorSIR = do
  pBeta  <- gamma 2 1 #Œ≤
  pGamma <- gamma 1 (1/8) #Œ≥
  return (TransParamsSIR pBeta pGamma)

-- | Transition model between S and I
transSI :: TransModel env sig m Double Popl
transSI beta (Popl s i r) = do
  let pop = s + i + r
  dN_SI <- binomial' s (1 - exp ((-beta * fromIntegral i) / fromIntegral pop))
  return $ Popl (s - dN_SI) (i + dN_SI) r

-- | Transition model between I and R
transIR :: TransModel env sig m Double Popl
transIR gamma (Popl s i r)  = do
  dN_IR <- binomial' i (1 - exp (-gamma))
  return $ Popl s (i - dN_IR) (r + dN_IR)

-- | Transition model between S, I, and R
transSIR :: Member (Writer [Popl]) sig
  => TransModel env sig m TransParamsSIR Popl
transSIR (TransParamsSIR beta gamma) sir = do
  sir' <- (transSI beta >=> transIR gamma) sir
  Model $ tell [sir']  -- a user effect for writing each latent SIR state to a stream [Popl]
  return sir'

-- | Observation model prior
obsPriorSIR :: Observables env '["œÅ"] Double
  => Model env sig m ObsParams
obsPriorSIR = do
  pRho <- beta 2 7 #œÅ
  return pRho

-- | Observation model from I to ùúâ
obsSIR :: Observable env "ùúâ" Int
  => ObsModel env sig m Double Popl Reported
obsSIR rho (Popl _ i _)  = do
  i <- poisson (rho * fromIntegral i) #ùúâ
  return i

-- | SIR as HMM
hmmSIR :: (Member (Writer [Popl]) sig, Observable env "ùúâ" Int, Observables env '["œÅ", "Œ≤", "Œ≥"] Double)
  => Int -> Popl -> Model env sig m Popl
hmmSIR = hmmGen transPriorSIR obsPriorSIR transSIR obsSIR

-- | Handle the user effect for writing each SIR state to a stream [Popl]
hmmSIR' :: (Observables env '["ùúâ"] Int , Observables env '[ "Œ≤" , "Œ≥" , "œÅ"] Double) => Int -> Popl -> Model env sig m ([Popl], Popl)
hmmSIR' n = runWriterM . hmmSIR n

-- | Simulating from SIR model: ([(s, i, r)], [ùúâ])
simulateSIR :: Sampler ([(Int, Int, Int)], [Reported])
simulateSIR = do
  -- Specify model input of 762 susceptible and 1 infected
  let sir_0      = Popl {s = 762, i = 1, r = 0}
  -- Specify model environment
      sim_env_in :: Env SIRenv
      sim_env_in = #Œ≤ := [0.7] <:> #Œ≥ := [0.009] <:> #œÅ := [0.3] <:> #ùúâ := [] <:> nil
  -- Simulate an epidemic over 100 days
  ((sir_trace, _), sim_env_out) <- SIM.simulate sim_env_in $ hmmSIR' 100 sir_0
  -- Get the observed infections over 100 days
  let ùúâs :: [Reported] = get #ùúâ sim_env_out
  -- Get the true SIR values over 100 days
      sirs = map (\(Popl s i recov) -> (s, i, recov)) sir_trace
  return (sirs, ùúâs)

-- | MH inference from SIR model: ([œÅ], [Œ≤])
inferSIR :: Sampler ([Double], [Double])
inferSIR = do
  -- Simulate some observed infections
  ùúâs <- snd <$> simulateSIR
  -- Specify model input of 762 susceptible and 1 infected
  let sir_0           = Popl {s = 762, i = 1, r = 0}
  -- Specify model environment
      mh_env_in :: Env SIRenv
      mh_env_in = #Œ≤ := [] <:> #Œ≥ := [0.0085] <:> #œÅ := [] <:> #ùúâ := ùúâs <:> nil
  -- Run MH inference over 50000 iterations
  mhTrace <- MH.mhRaw 5000 (hmmSIR' 100 sir_0) mh_env_in nil (#Œ≤ <:> #œÅ <:> nil)
  -- Get the sampled values for model parameters œÅ and Œ≤
  let œÅs = concatMap (get #œÅ) mhTrace
      Œ≤s = concatMap (get #Œ≤) mhTrace
  return (œÅs, Œ≤s)


{- | SIRS model.
-}
-- | SIRS model environment
type SIRSenv =
 '[ "Œ≤"  := Double  -- ^ mean contact rate between susceptible and infected people
  , "Œ≥"  := Double  -- ^ mean recovery rate
  , "Œ∑"  := Double  -- ^ rate of resusceptible
  , "œÅ"  := Double  -- ^ mean report rate of infection
  , "ùúâ"  := Int     -- ^ number of reported infections
 ]

-- | Transition model parameters
data TransParamsSIRS = TransParamsSIRS {
    betaP_SIRS  :: Double, -- ^ mean contact rate between susceptible and infected people
    gammaP_SIRS :: Double, -- ^ mean recovery rate
    etaP_SIRS   :: Double  -- ^ rate of resusceptible
}

-- | Transition model prior
transPriorSIRS :: Observables env '["Œ≤", "Œ∑", "Œ≥"] Double
  => Model env sig m TransParamsSIRS
transPriorSIRS = do
  TransParamsSIR pBeta pGamma  <- transPriorSIR
  pEta <- gamma 1 (1/8) #Œ∑
  return (TransParamsSIRS pBeta pGamma pEta)

-- | Transition model between R and S
transRS :: Double -> Popl -> Model env sig m Popl
transRS eta (Popl s i r) = do
  dN_RS <- binomial' r (1 - exp (-eta))
  return $ Popl (s + dN_RS) i (r - dN_RS)

-- | Transition model between S, to I, to R, and to S
transSIRS :: Member (Writer [Popl]) sig
  => TransModel env sig m TransParamsSIRS Popl
transSIRS (TransParamsSIRS beta gamma eta) sir = do
  sir' <- (transSI beta >=> transIR gamma >=> transRS eta) sir
  Model $ tell [sir']
  return sir'

-- | SIRS as HMM
hmmSIRS :: (Observables env '["ùúâ"] Int, Observables env '["Œ≤", "Œ∑", "Œ≥", "œÅ"] Double) => Int -> Popl -> Model env sig m ([Popl], Popl)
hmmSIRS n = runWriterM . hmmGen transPriorSIRS obsPriorSIR transSIRS obsSIR n

-- | Simulate from SIRS model: ([(s, i, r)], [ùúâ])
simulateSIRS :: Sampler ([(Int, Int, Int)], [Reported])
simulateSIRS = do
  -- Specify model input of 762 susceptible and 1 infected
  let sir_0      = Popl {s = 762, i = 1, r = 0}
  -- Specify model environment
      sim_env_in :: Env SIRSenv
      sim_env_in = #Œ≤ := [0.7] <:> #Œ≥ := [0.009] <:> #Œ∑ := [0.05] <:> #œÅ := [0.3] <:> #ùúâ := [] <:> nil
  -- Simulate an epidemic over 100 days
  ((sir_trace, _), sim_env_out) <- SIM.simulate sim_env_in $ hmmSIRS 100 sir_0
  -- Get the observed infections over 100 days
  let ùúâs :: [Reported] = get #ùúâ sim_env_out
  -- Get the true SIR values over 100 days
      sirs = map (\(Popl s i recov) -> (s, i, recov)) sir_trace
  return (sirs, ùúâs)


{- | SIRSV model.
-}
-- | SIRS model environment
type SIRSVenv =
 '[ "Œ≤"  := Double  -- ^ mean contact rate between susceptible and infected people
  , "Œ≥"  := Double  -- ^ mean recovery rate
  , "Œ∑"  := Double  -- ^ rate of resusceptible
  , "œâ"  := Double  -- ^ vaccination rate
  , "œÅ"  := Double  -- ^ mean report rate of infection
  , "ùúâ"  := Int     -- ^ number of reported infections
 ]

-- | Transition model parameters
data TransParamsSIRSV = TransParamsSIRSV {
    betaP_SIRSV  :: Double, -- ^ mean contact rate between susceptible and infected people
    gammaP_SIRSV :: Double, -- ^ mean recovery rate
    etaP_SIRSV   :: Double, -- ^ rate of resusceptible
    omegaP_SIRSV :: Double  -- ^ vaccination rate
}

-- | Latent state
data PoplV = PoplV {
    s' :: Int,  -- ^ susceptible individuals
    i' :: Int,  -- ^ infected individuals
    r' :: Int,  -- ^ recovered individuals
    v' :: Int   -- ^ vaccinated individuals
} deriving Show

-- | Transition from S to I
transSI' :: TransModel env sig m Double PoplV
transSI' beta (PoplV s i r v) = do
  let pop = s + i + r + v
  dN_SI <- binomial' s (1 - exp ((-beta * fromIntegral i) / fromIntegral pop))
  return $ PoplV (s - dN_SI) (i + dN_SI) r v

-- | Transition from I to R
transIR' :: TransModel env sig m Double PoplV
transIR' gamma (PoplV s i r v)  = do
  dN_IR <- binomial' i (1 - exp (-gamma))
  return $ PoplV s (i - dN_IR) (r + dN_IR) v

-- | Transition from R to S
transRS' :: TransModel env sig m Double PoplV
transRS' eta (PoplV s i r v) = do
  dN_RS <- binomial' r (1 - exp (-eta))
  return $ PoplV (s + dN_RS) i (r - dN_RS) v

-- | Transition from S to V
transSV' :: TransModel env sig m Double PoplV
transSV' omega (PoplV s i r v)  = do
  dN_SV <- binomial' s (1 - exp (-omega))
  return $  PoplV (s - dN_SV) i r (v + dN_SV )

-- | Transition between S to I, I to R, R to S, and S to V
transSIRSV :: Member (Writer [PoplV]) sig => TransModel env sig m TransParamsSIRSV PoplV
transSIRSV (TransParamsSIRSV beta gamma omega eta) sirv = do
  sirv' <- (transSI' beta  >=>
            transIR' gamma >=>
            transRS' eta   >=>
            transSV' omega) sirv
  Model $ tell [sirv']
  return sirv'

-- | Transition model prior
transPriorSIRSV :: Observables env '["Œ≤", "Œ≥", "œâ", "Œ∑"] Double
  => Model env sig m TransParamsSIRSV
transPriorSIRSV  = do
  TransParamsSIRS pBeta pGamma pEta <- transPriorSIRS
  pOmega <- gamma 1 (1/16) #œâ
  return (TransParamsSIRSV pBeta pGamma pEta pOmega)

-- | Observation model
obsSIRSV :: Observable env "ùúâ" Int
  => ObsModel env sig m Double PoplV Reported
obsSIRSV rho (PoplV _ i _ v)  = do
  i <- poisson (rho * fromIntegral i) #ùúâ
  return i

-- | SIRSV as HMM
hmmSIRSV ::  (Observables env '["ùúâ"] Int, Observables env '["Œ≤", "Œ≥", "Œ∑", "œâ", "œÅ"] Double) => Int -> PoplV -> Model env sig m ([PoplV], PoplV)
hmmSIRSV n = runWriterM . hmmGen transPriorSIRSV obsPriorSIR transSIRSV obsSIRSV n

-- | Simulate from SIRSV model : ([(s, i, r, v)], [ùúâ])
simulateSIRSV :: Sampler ([(Int, Int, Int, Int)], [Reported])
simulateSIRSV = do
  -- Specify model input of 762 susceptible and 1 infected
  let sirv_0      = PoplV {s' = 762, i' = 1, r' = 0, v' = 0}
  -- Specify model environment
      sim_env_in :: Env SIRSVenv
      sim_env_in = #Œ≤ := [0.7] <:> #Œ≥ := [0.009] <:> #Œ∑ := [0.05] <:> #œâ := [0.02] <:> #œÅ := [0.3] <:> #ùúâ := [] <:> nil
  -- Simulate an epidemic over 100 days
  ((sirv_trace, _), sim_env_out) <- SIM.simulate sim_env_in $ hmmSIRSV 100 sirv_0
  -- Get the observed infections over 100 days
  let ùúâs :: [Reported] = get #ùúâ sim_env_out
  -- Get the true SIRV values over 100 days
      sirvs = map (\(PoplV s i recov v) -> (s, i, recov, v)) sirv_trace
  return (sirvs, ùúâs)
